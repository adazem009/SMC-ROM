clear
// BIOS version
set/"ver","0.1.0-rc"
// Functions
define/"_import"
{
	// _import
	// (no arguments)
	// Setup screen
	clear
	bgcolor/"000000000"
	// Ask for PC code
	read/"Paste your PC code: ","code"
	// Load the PC code
	print/"Importing...",\n
	loadcode/code
	print/"Done!",\n
}
define/"_setup_menu_render"
{
	// _setup_menu_render
	// (no arguments)
	warp
		clear
		set/"r",1
		// Header
		print/"SMC Computer stock BIOS",\n
		print/"Version: ",ver,\n,\n
		// Menu
		print/"Choose a number:",\n
		set/"si",0
		repeat/menulen
			calc/"si=si+1"
			getitem/"menu",si/"it"
			print/si,") ",it,\n
		endloop
		// Footer
		print/"*dangerous",\n
	endwarp
}
define/"_setup_menu"
{
	// _setup_menu
	// (no arguments)
	hidelogo
	setlist/"menu"/"Continue boot","Reboot","Load PC code*"
	getlistlength/"menu"/"menulen"
	set/"exit",0
	while/[exit == 0]
		// Render menu
		_setup_menu_render
		read/"","sel"
		if/[sel == 1]
			set/"exit",1
		else
			if/[sel == 2]
				reboot
			else
				if/[sel == 3]
					_import
				endif
			endif
		endif
	endloop
}
bgcolor/"000000000"
showlogo
enabletext
print/"Hold S to enter setup...",\n
// Read BIOS settings
getitem/"arg_bin",1/"f"
wait/0.5
getkey/"keys"
getitem/"keys",1/"key"
if/[key == "s"]
	// Enter setup menu
	print/"Entering setup...",\n
	_setup_menu
	bgcolor/"000000000"
	clear
endif
clear
// List disks
listdisk/"disks"
getlistlength/"disks"/"diskcount"
// Ask for PC code if no disks are found
if/[diskcount == 0]
	print/"Error: No disks found.",\n
	_import
	reboot
endif
// Try to boot from every disk
// If this, fails, show an error message
set/"i",0
while/[i < diskcount]
	// Boot from disk
	calc/"i=i+1"
	// Read MBR
	cpdisk/i/"mbr"
	// Leave BIOS-space
	leavebios
	// Execute MBR
	run/mbr,"bg",1,4096/i
endloop
wait/1
// Wait until all processes stop
while/[%ps > 0]
	wait/10
endloop
// Boot failure
warp
	clear
	print>
		"Error: Failed to boot."
		""
		"Please check the PC code you used."
	<print
endwarp
wait/10
shutdown
